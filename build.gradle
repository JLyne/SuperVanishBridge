import org.apache.tools.ant.filters.ReplaceTokens

allprojects {
    group 'uk.co.notnull'
    version '1.0-SNAPSHOT'
}

if (file("./build.gradle.local").exists()) {
    apply from: "./build.gradle.local"
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()

        maven {
            url 'https://papermc.io/repo/repository/maven-public/'
        }

        maven {
            url 'https://jitpack.io/'
        }
    }

    java.sourceCompatibility = JavaVersion.VERSION_17
    java.targetCompatibility = JavaVersion.VERSION_17

    tasks {
        processResources {
            from("${project.rootDir}/src/main/resources") {
                project.properties.findAll().each {
                    prop ->
                        if (prop.value != null && prop.value instanceof java.lang.String) {
                            filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                            filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                        }
                }
            }
        }

        compileJava {
            options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
            options.encoding = 'UTF-8'
        }
    }
}
